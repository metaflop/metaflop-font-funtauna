% A
newchar("0041",560u#+stem_cap#,ht#,0,
 .1gap_cap#+.7jut_cap#,.1gap_cap#+.7jut_cap#);
z1l=(l,0); 
z4r=(r,0); 
z5=(.5[x1l,x4r],h-min(band_cap,.9stem_cap));
y2r=y3l=h; 
posin1(1,.9stem_cap,1,x5-x1l,y5-y1l);
posin4(1,.9stem_cap,1,x5-x4r,y5-y4r);
z2r=whatever[z1r,z5];
z3l=whatever[z4l,z5];
posin2(1,.9stem_cap,1,x5-x1l,y5-y1l);
posin3(1,.9stem_cap,1,x5-x4r,y5-y4r);
y6r=y7r=.36h+band_cap;
pos6(band_cap,right,angle(z2-z1)); 
pos7(band_cap,right,angle(z3-z4)); 
z6=whatever[z1,z2];
z7=whatever[z3,z4]; 
penstroke z6e--z7e;
serifsarm(2,1,a,b,.8jut_cap,.8jut_cap,band_cap);
serifsarm(3,4,c,d,.8jut_cap,.8jut_cap,band_cap);
charanchortops_[charepscode]=(.5[l,r],h);
charanchorbots_[charepscode]=(.5[l,r],0);
charanchorcedillas_[charepscode]=z4;
penlabels(range 1 thru 7); 
endchar;

% B
newchar("0042",360u#+stem_cap#,ht#,0,
 gap_cap#+jut_cap#,.75gap_cap#+.375jut_cap#);
x6l=x7l=l;
x1=x5=x11=x6;
x9r=r;
x3r=.97[l,r];
x2=x4=x8=x10=.5[x3,x6l];
y5=.52h;
y4l=y5l;
y8l=y5r;
y1r=y2r=y7=h;
y6=y10r=y11r=0;
y3=.5[y4,y2];
y9=.5[y10,y8];
pos1(band_cap,left,90);
pos2(band_cap,left,90);
pos3(stem_cap,up,0);
pos4(.6band_cap,right,-90);
pos5(band_cap,right,-90);
pos6(stem_cap,up,0);
pos7(stem_cap,up,0);
pos8(.6band_cap,left,90);
pos9(stem_cap,up,0);
pos10(band_cap,right,-90);
pos11(band_cap,right,-90);
penstroke z5e--z4e & arc.e(4,3) & arc.e(3,2) & z2e--z1e;
penstroke z11e--z10e & arc.e(10,9) & arc.e(9,8) & z8e--z5r;
penstroke z6e--z7e; 
serif(7,6,a,.8jut_cap,band_cap);
serif(6,7,b,-.8jut_cap,band_cap);
penlabels(range 1 thru 11);
endchar;

% C
newchar("0043",460u#+.5stem_cap#,ht#,0,
 .75gap_cap#+.375jut_cap#,.75gap_cap#+.3jut_cap#);
x3r=l;
x1r=x5r=r;
x2=x4=.5[x3,r];
y2r=h;
y4r=0;
y3=.5h;
y1r=.7h;
y5r=.3h;
pos1(stem_cap,up,0);
pos2(band_cap,left,90);
pos3(stem_cap,down,180);
pos4(band_cap,right,-90);
pos5(stem_cap,up,0);
penstroke arc.e(1,2) & arc.e(2,3) & arc.e(3,4) & arc.e(4,5);
charanchorcedillas_[charepscode]=z4r;
charanchortops_[charepscode]=(.5[l,r],h);
penlabels(range 1 thru 5);
endchar;

% D
newchar("0044",420u#+stem_cap#,ht#,0,
 gap_cap#+jut_cap#,.75gap_cap#+.375jut_cap#);
x6l=x7l=l;
x1=x5=x6;
x3r=r;
x2=x4=.5[x3,x6l];
y1r=y2r=y7=h;
y4r=y5r=y6=0;
y3=.5[y4,y2];
pos1(band_cap,left,90);
pos2(band_cap,left,90);
pos3(stem_cap,up,0);
pos4(band_cap,right,-90);
pos5(band_cap,right,-90);
pos6(stem_cap,up,0);
pos7(stem_cap,up,0);
penstroke z5e--z4e & arc.e(4,3) & arc.e(3,2) & z2e--z1e;
penstroke z6e--z7e; 
serif(7,6,a,jut_cap,band_cap);
serif(6,7,b,-jut_cap,band_cap);
charanchortops_[charepscode]=(.4[l,r],h);
charanchorbots_[charepscode]=(x6,0);
penlabels(range 1 thru 7);
endchar;

% E
newchar("0045",400u#+.5stem_cap#,ht#,0,
 gap_cap#+jut_cap#,.5gap_cap#+.25jut_cap#);
x1l=x2l=l;
x5=x7=x3r=x1;
x4=.95[x1,r];
x6=.75[x1,r];
x8=r;
y1=y7r=y8r=0;
y2=y3l=y4l=h;
y5=y6=.52h;
pos1(stem_cap,up,0);
pos2(stem_cap,up,0);
pos3(band_cap,right,-90);
pos4(band_cap,right,-90);
pos5(band_cap,right,-90);
pos6(band_cap,right,-90);
pos7(band_cap,right,-90);
pos8(band_cap,right,-90);
penstroke z1e--z2e;
penstroke z3e--z4e;
penstroke z5e--z6e;
penstroke z7e--z8e;
serif(2,1,a,jut_cap,band_cap);
serif(1,2,b,-jut_cap,band_cap);
serif(3,4,c,.9jut_cap,stem_cap);
serif(7,8,d,-jut_cap,stem_cap);
charanchortops_[charepscode]=(.4[l,r],h);
charanchorbots_[charepscode]=(.5[l,r],0);
charanchorcedillas_[charepscode]=(.7[x7,x8],0);
penlabels(range 1 thru 8); 
endchar;

% F
newchar("0046",380u#+.5stem_cap#,ht#,0,
 gap_cap#+jut_cap#,.4gap_cap#+.25jut_cap#);
x1l=x2l=l;
x3r=x5=x1;
x4=r;
x6=.8[x1,x4];
y1=0;
y2=y3l=y4l=h;
y5=y6=.48h;
pos1(stem_cap,up,0);
pos2(stem_cap,up,0);
pos3(band_cap,right,-90);
pos4(band_cap,right,-90);
pos5(band_cap,right,-90);
pos6(band_cap,right,-90);
penstroke z1e--z2e;
penstroke z3e--z4e;
penstroke z5e--z6e;
serifs(2,1,a,b,jut_cap,jut_cap,band_cap);
serif(1,2,c,-jut_cap,band_cap);
serif(3,4,d,.9jut_cap,stem_cap);
penlabels(range 1 thru 6); 
endchar;

% G
newchar("0047",480u#+.5stem_cap#,ht#,0,
 .75gap_cap#+.375jut_cap#,.75gap_cap#+.375jut_cap#);
x3r=l;
x1r=x5=r;
x2=x4=.5[x3,x1];
y2r=h;
y4r=y5r=0;
y3=.52h;
y1r=.7h;
z6r=z5;
z7r=z8l=(x6r,.45h);
z9l=(.55w,y8l);
pos1(stem_cap,up,0);
pos2(band_cap,left,90);
pos3(stem_cap,down,180);
pos4(band_cap,right,-90);
pos5(band_cap,right,-90);
pos6(stem_cap,up,0);
pos7(stem_cap,up,0);
pos8(band_cap,right,-90);
pos9(band_cap,right,-90);
penstroke arc.e(1,2) & arc.e(2,3) & arc.e(3,4) & z4e--z5e;
penstroke z6e--z7e;
penstroke z8e--z9e;
charanchortops_[charepscode]=(x2,h);
charanchorbots_[charepscode]=(x4,0);
penlabels(range 1 thru 9);
endchar;

% H
newchar("0048",440u#+stem_cap#,ht#,0,
 gap_cap#+jut_cap#,gap_cap#+jut_cap#);
x1l=x2l=l;
x3r=x4r=r;
x5=x1;
x6=x3;
y1l=y3r=0;
y2l=y4r=h;
y5=y6=.51h;
pos1(stem_cap,up,0);
pos2(stem_cap,up,0);
pos3(stem_cap,up,0);
pos4(stem_cap,up,0);
pos5(band_cap,right,-90);
pos6(band_cap,right,-90);
penstroke z1e--z2e;
penstroke z3e--z4e;
penstroke z5e--z6e;
serifs(2,1,a,b,jut_cap,jut_cap,band_cap);
serifs(1,2,c,d,jut_cap,jut_cap,band_cap);
serifs(4,3,e,f,jut_cap,jut_cap,band_cap);
serifs(3,4,g,h,jut_cap,jut_cap,band_cap);
charanchortops_[charepscode]=(.5[l,r],h);
penlabels(range 1 thru 6); 
endchar;

% I
newchar("0049",stem_cap#,ht#,0,
 gap_cap#+jut_cap#,gap_cap#+jut_cap#);
x1=x2=.5w;
y1=0;
y2=h;
pos1(stem_cap,up,0);
pos2(stem_cap,up,0);
penstroke z1e--z2e;
serifs(2,1,a,b,jut_cap,jut_cap,band_cap);
serifs(1,2,c,d,jut_cap,jut_cap,band_cap);
charanchortops_[charepscode]=(.5w,h);
charanchorbots_[charepscode]=(.5w,0);
charanchorcedillas_[charepscode]=(.5w,0);
penlabels(range 1 thru 2);
endchar;

% J
newchar("004A",280u#+stem_cap#,ht#,0,
 .5gap_cap#,.9gap_cap#+.9jut_cap#);
x1r=x2r=r;
x4r=l;
x3r=.5[l,r];
y3r=0;
y2r=h;
y1r=y4r=.3h;
pos1(stem_cap,up,0);
pos2(stem_cap,up,0);
pos3(band_cap,right,-90);
pos4(stem_cap,down,180);
penstroke arc.e(4,3) & arc.e(3,1) & z1e--z2e;
serifs(1,2,a,b,.9jut_cap,1.5jut_cap,band_cap);
charanchortops_[charepscode]=(x2,h);
penlabels(range 1 thru 4);
endchar;

% K
newchar("004B",400u#+1.8stem_cap#,ht#,0,
 gap_cap#+jut_cap#,.15jut_cap#);
x1l=x2l=l;
x6r=r;
x3r=.9[x1,x6r];
x4l=x5l=x1;
y1=y6r=0;
y2=y3r=h;
y4l=y5l=.52h;
pos1(stem_cap,up,0);
pos2(stem_cap,up,0);
posin3(1,.9stem_cap,1,x3r-x4l,y3r-y4l);
pos4(.9stem_cap,z3l-z4l,0);
posin6(1,.9stem_cap,1,x6r-x5l,y6r-y5l);
z5r=z6r+whatever*(z6l-z5l)=whatever[z3,z4];
z5=z6+whatever*(z6l-z5l)=z5l+whatever*right;
penstroke z1e--z2e;
serifs(1,2,a,h,.8jut_cap,jut_cap,band_cap);
serifs(2,1,b,c,jut_cap,.8jut_cap,band_cap);
serifsarm(4,3,d,e,.8jut_cap,.8jut_cap,band_cap);
serifsarm(5,6,f,g,.8jut_cap,.8jut_cap,band_cap);
charanchorbots_[charepscode]=(.5[x1,x6l],0);
penlabels(range 1 thru 7);
endchar;

% L
newchar("004C",400u#+.5stem_cap#,ht#,0,
 gap_cap#+jut_cap#,.4gap_cap#+.25jut_cap#);
x1l=x2l=l;
x4=r;
y1=y4r=0;
y2=h;
z3r=z1;
pos1(stem_cap,up,0);
pos2(stem_cap,up,0);
pos3(band_cap,right,-90);
pos4(band_cap,right,-90);
penstroke z1e--z2e;
penstroke z3e--z4e;
serif(2,1,a,jut_cap,band_cap);
serifs(1,2,b,c,jut_cap,jut_cap,band_cap);
serif(3,4,d,-1.5jut_cap,stem_cap);
charanchortops_[charepscode]=(x1,h);
charanchortoprights_[charepscode]=(x1r+40u+jut_cap,ht_x);
charanchorbots_[charepscode]=(.4[x1,x4],0);
charanchorcedillas_[charepscode]=(.8[x1,x4],0);
penlabels(range 1 thru 4); 
endchar;

% M
newchar("004D",620u#+stem_cap#,ht#,0,
 gap_cap#+jut_cap#,gap_cap#+jut_cap#);
if mono=1: stem_cap:=.9stem_cap; fi
x1l=x2l=l;
x3r=x4r=r;
x5l=x1r;
x6r=.5[x1r,x3l];
x9r=x4l;
y1l=y3r=0;
y8=y10=.4h*mono;
y2l=y4r=y7=y11=h;
y2l-y5l=y4r-y9r=y6r-y10=min(band_cap,.9stem_cap);
pos1(stem_cap,up,0);
pos2(stem_cap,up,0);
pos3(stem_cap,up,0);
pos4(stem_cap,up,0);
posin5(1,.9stem_cap,1,x6r-x5l,y6r-y5l);
posin7(1,.9stem_cap,1,x6r-x5l,y6r-y5l);
posin8(1,.9stem_cap,1,x6r-x5l,y6r-y5l);
posin9(1,.9stem_cap,1,x9r-x6r,y9r-y6r);
posin10(1,.9stem_cap,1,x9r-x6r,y9r-y6r);
posin11(1,.9stem_cap,1,x9r-x6r,y9r-y6r);
z7r=whatever[z5r,z6r];
z8r=whatever[z5r,z6r];
z10l=whatever[z6r,z9l];
z11l=whatever[z6r,z9l];
penstroke z1e--z2e;
penstroke z3e--z4e;
fill z2--z5l--z8l--z10r--z9r--z4--z11l--z6r--z7r--cycle;
serifs(2,1,a,b,jut_cap,.8jut_cap,band_cap);
serif(1,2,c,-jut_cap,band_cap);
serif(3,4,d,jut_cap,band_cap);
serifs(4,3,e,f,.8jut_cap,jut_cap,band_cap);
if mono=1: stem_cap:=stem_cap/.9; fi
penlabels(range 1 thru 11); 
endchar;

% N
newchar("004E",480u#+stem_cap#,ht#,0,
 gap_cap#+jut_cap#,gap_cap#+jut_cap#);
x1l=x2l=l;
x3r=x4r=r;
x5l=x1r;
x6r=x3l;
y1l=y3r=y8=0;
y2l=y4r=y7=h;
y2l-y5l=y6r=min(band_cap,.9stem_cap);
pos1(stem_cap,up,0);
pos2(stem_cap,up,0);
pos3(stem_cap,up,0);
pos4(stem_cap,up,0);
posin5(1,.9stem_cap,1,x6r-x5l,y6r-y5l);
posin6(1,.9stem_cap,1,x6r-x5l,y6r-y5l);
posin7(1,.9stem_cap,1,x6r-x5l,y6r-y5l);
posin8(1,.9stem_cap,1,x6r-x5l,y6r-y5l);
z7=whatever[z5,z6];
z8=whatever[z5,z6];
penstroke z1e--z2e;
penstroke z3e--z4e;
fill z2--z5l--z8l--z3--z6r--z7r--cycle;
serifs(2,1,a,b,jut_cap,jut_cap,band_cap);
serif(1,2,c,-jut_cap,band_cap);
serifs(3,4,d,e,jut_cap,jut_cap,band_cap);
charanchortops_[charepscode]=(.5[l,r],h);
charanchorbots_[charepscode]=(.5[l,r],0);
penlabels(range 1 thru 8); 
endchar;

% Eng
newchar("014A",440u#+stem_cap#,ht#,depth_desc#,
 gap_cap#+jut_cap#,.75gap_cap#+.375jut_cap#);
x1l=x2l=l;
x5r=x6r=r;
x4=.5[x1,x6];
x7=.7[x1,x5];
x8=.4[x1,x5];
y1l=y6r=0;
y2l=y4r=h;
y5r=.5h;
y7r=y8r=-d;
z3r=z2;
pos1(stem_cap,up,0);
pos2(stem_cap,up,0);
pos3(band_cap,left,90);
pos4(band_cap,left,90);
pos5(stem_cap,up,0);
pos6(stem_cap,up,0);
pos7(band_cap,right,-90);
pos8(band_cap,right,-90);
penstroke z1e--z2e;
penstroke z8e--z7e & arc.e(7,6) & z6e--z5e & arc.e(5,4) & z4e--z3e;
serif(1,2,a,-jut_cap,band_cap);
serifs(2,1,b,c,jut_cap,1.1jut_cap,band_cap);
charanchortops_[charepscode]=(x4,h);
charanchorbots_[charepscode]=(x4,0);
penlabels(range 1 thru 6);
endchar;

% O
newchar("004F",500u#+stem_cap#,ht#,0,
 .75gap_cap#+.375jut_cap#,.75gap_cap#+.375jut_cap#);
z1r=(r,.5h);
z2r=(.5[x1r,x3r],h);
z3r=(l,.5h);
z4r=(.5[x1r,x3r],0);
pos1(stem_cap,up,0);
pos2(band_cap,left,90);
pos3(stem_cap,down,180);
pos4(band_cap,right,-90);
penstroke arc.e(1,2) & arc.e(2,3) & arc.e(3,4) & arc.e(4,1) & cycle; 
charanchortops_[charepscode]=(.5[l,r],h);
charanchorbots_[charepscode]=(.5[l,r],0);
penlabels(range 1 thru 4);
endchar;

% P
newchar("0050",360u#+stem_cap#,ht#,0,
 gap_cap#+jut_cap#,.5gap_cap#+.25jut_cap#);
x6l=x7l=l;
x1=x5=x6;
x3r=r;
x2=x4=.5[x3l,x6r];
y4=y5=.5h;
y1r=y2r=y7=h;
y6l=0;
y3r=.5[y4,y2];
pos1(band_cap,left,90);
pos2(band_cap,left,90);
pos3(stem_cap,up,0);
pos4(band_cap,right,-90);
pos5(band_cap,right,-90);
pos6(stem_cap,up,0);
pos7(stem_cap,up,0);
penstroke z5e--z4e & arc.e(4,3) & arc.e(3,2) & z2e--z1e;
penstroke z6e--z7e; 
serifs(7,6,a,b,jut_cap,1.1jut_cap,band_cap);
serif(6,7,c,-jut_cap,band_cap);
penlabels(range 1 thru 7);
endchar;

% Q
newchar("0051",500u#+stem_cap#,ht#,.9depth_desc#,
 .75gap_cap#+.375jut_cap#,.75gap_cap#+.375jut_cap#);
z1r=(r,.5h);
z2r=(.5[l,r],h);
z3r=(l,.5h);
z4r=(.5[l,r],0);
z5=z4;
z6=(x4,.5[y7l,y4r]);
z7r=(.4[x4,x1],-d);
z8r=(.6[x4,x1],-d);
pos1(stem_cap,up,0);
pos2(band_cap,left,90);
pos3(stem_cap,down,180);
pos4(band_cap,right,-90);
pos5(stem_cap,down,180);
pos6(stem_cap,down,180);
pos7(band_cap,right,-90);
pos8(band_cap,right,-90);
penstroke arc.e(1,2) & arc.e(2,3) & arc.e(3,4) & arc.e(4,1) & cycle; 
penstroke z5e--z6e & arc.e(6,7) & z7e--z8e; 
penlabels(range 1 thru 8);
endchar;

% R
newchar("0052",400u#+stem_cap#,ht#,0,
 gap_cap#+jut_cap#,.2gap_cap#+.5jut_cap#);
x6l=x7l=l;
x1=x5=x6;
x9r=r;
x3r=if mono=1: r else: .9[l,r] fi;
x2=x4=.5[x3,x6l];
x8r=.3[x4,x3l];
y4=y5=.5h;
y1r=y2r=y7l=h;
y6l=y9r=0;
y3r=.5[y4,y2];
y8r=y4l;
pos1(band_cap,left,90);
pos2(band_cap,left,90);
pos3(stem_cap,up,0);
pos4(band_cap,right,-90);
pos5(band_cap,right,-90);
pos6(stem_cap,up,0);
pos7(stem_cap,up,0);
pos8(.9stem_cap,z8r-z9r,0);
pos9(.9stem_cap,z8r-z9r,0);
penstroke z5e--z4e & arc.e(4,3) & arc.e(3,2) & z2e--z1e;
penstroke z6e--z7e; 
serifs(7,6,a,b,jut_cap,jut_cap,band_cap);
serif(6,7,c,-jut_cap,band_cap);
serifarm(8,9,d,-jut_cap,band_cap);
charanchortops_[charepscode]=(.4w,h);
charanchorbots_[charepscode]=(.5w,0);
penlabels(range 1 thru 9);
endchar;

% S
newchar("0053",380u#+stem_cap#,ht#,0,
 .75gap_cap#+.375jut_cap#,.75gap_cap#+.375jut_cap#);
x3r=x8r=l;
x6r=r;
x2=x4=x5=x7=.5[l,r];
x1r=.98[l,r];
y2r=h;
y7r=0;
y4=y5=.5h;
y1r=y3=.5[y4,y2];
y6=y8r=.5[y7,y5];
pos1(stem_cap,up,0);
pos2(band_cap,left,90);
pos3(stem_cap,down,180);
pos4(band_cap,right,-90);
pos5(band_cap,left,90);
pos6(stem_cap,up,0);
pos7(band_cap,right,-90);
pos8(stem_cap,down,180);
penstroke arc.e(1,2) & arc.e(2,3) & arc.e(3,4);
penstroke arc.e(8,7) & arc.e(7,6) & arc.e(6,5);
charanchortops_[charepscode]=(x7,h);
charanchorbots_[charepscode]=(x7,0);
charanchorcedillas_[charepscode]=(x7,0);
penlabels(range 1 thru 8);
endchar;

% T
newchar("0054",580u#,ht#,0,
 .1gap_cap#+.3jut_cap#,.1gap_cap#+.3jut_cap#);
x1=x2=.5[l,r];
x3=l;
x4=r;
y1=0;
y2=h;
y3l=y4l=h;
pos1(stem_cap,up,0);
pos2(stem_cap,up,0);
pos3(band_cap,right,-90);
pos4(band_cap,right,-90);
penstroke z1e--z2e;
penstroke z3e--z4e;
serifs(2,1,a,b,jut_cap,jut_cap,band_cap);
serif(4,3,c,-jut_cap,stem_cap);
serif(3,4,d,jut_cap,stem_cap);
charanchortops_[charepscode]=(x2,h);
charanchorbots_[charepscode]=(x1,0);
charanchorcedillas_[charepscode]=(x1,0);
penlabels(range 1 thru 4);
endchar;

% U
newchar("0055",460u#+stem_cap#,ht#,0,
 gap_cap#+jut_cap#,gap_cap#+jut_cap#);
x1r=x2r=l;
x3r=x4r=r;
x5r=.5[x1r,x3r];
y1r=y3r=.35h;
y2r=y4r=h;
y5r=0;
pos1(stem_cap,down,180);
pos2(stem_cap,down,180);
pos3(stem_cap,up,0);
pos4(stem_cap,up,0);
pos5(band_cap,right,-90);
penstroke z2e--z1e & arc.e(1,5) & arc.e(5,3) & z3e--z4e;
serifs(1,2,c,d,jut_cap,jut_cap,band_cap);
serifs(3,4,g,h,jut_cap,jut_cap,band_cap);
charanchortops_[charepscode]=(x5,h);
charanchorbots_[charepscode]=(x5,0);
charanchorcedillas_[charepscode]=(x5,0);
penlabels(range 1 thru 6); 
endchar;

% V
newchar("0056",520u#+stem_cap#,ht#,0,
 .1gap_cap#+.7jut_cap#,.1gap_cap#+.7jut_cap#);
z1l=(l,h); 
z4r=(r,h); 
z5=(.5[x1l,x4r],min(band_cap,.9stem_cap));
y2r=y3l=0; 
posin1(1,.9stem_cap,1,x5-x1l,y5-y1l);
posin4(1,.9stem_cap,1,x5-x4r,y5-y4r);
z2r=whatever[z1r,z5];
z3l=whatever[z4l,z5];
posin2(1,.9stem_cap,1,x5-x1l,y5-y1l);
posin3(1,.9stem_cap,1,x5-x4r,y5-y4r);
serifsarm(2,1,a,b,.8jut_cap,.8jut_cap,band_cap);
serifsarm(3,4,c,d,.8jut_cap,.8jut_cap,band_cap);
penlabels(range 1 thru 5); 
endchar;

% W
newchar("0057",780u#+1.2stem_cap#,ht#,0,
 .1gap_cap#+.7jut_cap#,.1gap_cap#+.7jut_cap#);
if mono=1: stem_cap:=.9stem_cap; fi
z1l=(l,h); 
z4r=(.5[l,r]+.2stem_cap,if mono=1: .6h else: h fi); 
z5=(.5[x1l,x4r],min(band_cap,.9stem_cap));
y2r=y3l=0; 
posin1(1,.9stem_cap,1,x5-x1l,y5-y1l);
posin4(1,.9stem_cap,1,x5-x4r,y5-y4r);
z2r=whatever[z1r,z5];
z3l=whatever[z4l,z5];
posin2(1,.9stem_cap,1,x5-x1l,y5-y1l);
posin3(1,.9stem_cap,1,x5-x4r,y5-y4r);
z6l=(r-x4r+l,y4r);
z6r=(r-x4l+l,y4l);
z7l=(r-x3r+l,y3r);
z7r=(r-x3l+l,y3l);
z8l=(r-x2r+l,y2r);
z8r=(r-x2l+l,y2l);
z9l=(r-x1r+l,y1r);
z9r=(r-x1l+l,y1l);
z8=.5[z8l,z8r];
z9=.5[z9l,z9r];
penstroke z3e--z4e;
penstroke z6e--z7e;
serifsarm(2,1,a,b,.8jut_cap,.8jut_cap,band_cap);
serifsarm(8,9,c,d,.8jut_cap,.8jut_cap,band_cap);
charanchortops_[charepscode]=(.5[x1,x9],h);
charanchorbots_[charepscode]=(.5[x1,x9],0);
if mono=1: stem_cap:=stem_cap/.9; fi
penlabels(range 1 thru 9); 
endchar;

% X
newchar("0058",420u#+2stem_cap#,ht#,0,
 .1gap_cap#+.7jut_cap#,.1gap_cap#+.7jut_cap#);
x1l=x3l=l;
x2r=x4r=r;
y1l=y4r=0;
y2r=y3l=h;
posin1(1,.9stem_cap,1,x2r-x1l,y2r-x1l);
posin2(1,.9stem_cap,1,x2r-x1l,y2r-x1l);
posin3(1,.9stem_cap,1,x4r-x3l,y4r-y3l);
posin4(1,.9stem_cap,1,x4r-x3l,y4r-y3l);
serifsarms(2,1,a,b,c,d,.8jut_cap,.8jut_cap,band_cap);
serifsarms(3,4,e,f,g,h,.8jut_cap,.8jut_cap,band_cap);
charanchortops_[charepscode]=(.5[l,r],h);
charanchorbots_[charepscode]=(.5[l,r],0);
penlabels(range 1 thru 4); 
endchar;

% Y
newchar("0059",520u#+stem_cap#,ht#,0,
 .4gap_cap#+.25jut_cap#,.4gap_cap#+.25jut_cap#);
% like V
z1l=(l,h); 
z4r=(r,h); 
z5=(.5[x1l,x4r],.45h+min(band_cap,.9stem_cap));
y2r=y3l=.45h; 
posin1(1,.9stem_cap,1,x5-x1l,y5-y1l);
posin4(1,.9stem_cap,1,x5-x4r,y5-y4r);
z2r=whatever[z1r,z5];
z3l=whatever[z4l,z5];
posin2(1,.9stem_cap,1,x5-x1l,y5-y1l);
posin3(1,.9stem_cap,1,x5-x4r,y5-y4r);
% additions
z7=(.5w,0);
pos7(stem_cap,up,0);
z6l=whatever[z1l,z2l]=z7l+whatever*up;
z6r=whatever[z3r,z4r]=z7r+whatever*up;
z6=.5[z6l,z6r];
z8r=z9l=z5;
z8r-z8l=2*(z8r-z8)=z1r-z1l;
z9r-z9l=2*(z9r-z9)=z4r-z4l;
fill z6r--z9r--z8l--z6l--cycle;
serifsarm(8,1,a,b,.7jut_cap,.7jut_cap,band);
serifsarm(9,4,c,d,.7jut_cap,.7jut_cap,band);
serifsarm(6,7,e,f,jut_cap,jut_cap,band);
charanchortops_[charepscode]=(.5w,h);
charanchorbots_[charepscode]=(.5w,0);
penlabels(range 1 thru 9); 
endchar;

% Z
newchar("005A",550u#,ht#,0,
 .7gap_cap#+.25jut_cap#,.7gap_cap#+.25jut_cap#);
x4l=x5=l;
x6=r;
x1=.05[l,r];
x2=x3r=.98[l,r];
y1l=y2l=h;
y3r=y2r;
y4l=y5l;
y5r=y6r=0;
pos1(band_cap,right,-90);
pos2(band_cap,right,-90);
pos5(band_cap,right,-90);
pos6(band_cap,right,-90);
posin3(1,.9stem_cap,1,x3r-x4l,y3r-y4l);
posin4(1,.9stem_cap,1,x3r-x4l,y3r-y4l);
penstroke z1e--z2e;
penstroke z3e--z4e;
penstroke z5e--z6e;
serif(2,1,a,-1.5jut_cap,stem_cap);
serif(5,6,b,-1.5jut_cap,stem_cap);
charanchortops_[charepscode]=(.5[l,r],h);
charanchorbots_[charepscode]=(.5[l,r],0);
penlabels(range 1 thru 6); 
endchar;

% a
newchar("0061",290u#+stem#,ht_x#,0,
 .7gap#+.35jut#,.8gap#+jut#);
x6r=x7r=r;
x1=x5r=x7;
x3r=l;
x2=x4=.5[x3,x6l];
x8=.5[x3,x6l];
x9r=.04[l,r];
y4r=y5r=y6=0;
y1=y2=.52h;
y7=.65h;
y3r=.5[y4,y2];
y8r=h;
y9r=.5[y2,y8];
pos1(band,left,90);
pos2(band,left,90);
pos3(stem,down,180);
pos4(band,right,-90);
pos5(band,right,-90);
pos6(stem,up,0);
pos7(stem,up,0);
pos8(band,left,90);
pos9(stem,down,180);
penstroke z1e--z2e & arc.e(2,3) & arc.e(3,4) & z4e--z5e;
penstroke z6e--z7e & arc.e(7,8) & arc.e(8,9); 
serif(7,6,a,-jut,band);
charanchortops_[charepscode]=(.5[x3,x7],h);
charanchorbots_[charepscode]=(.6[x3,x7],0);
charanchorcedillas_[charepscode]=z6;
penlabels(range 1 thru 9);
endchar;

% b
newchar("0062",310u#+stem#,ht_asc#,0,
 gap#+jut#,.75gap#+.35jut#);
x6l=x7l=l;
x1=x5=x6;
x3r=r;
x2=x4=.5[x3l,x6r];
y4r=y5r=y6r=0;
y1r=y2r=ht_x;
y7r=h;
y3r=.5[y4,y2];
pos1(band,left,90);
pos2(band,left,90);
pos3(stem,up,0);
pos4(band,right,-90);
pos5(band,right,-90);
pos6(stem,up,0);
pos7(stem,up,0);
penstroke z5e--z4e & arc.e(4,3) & arc.e(3,2) & z2e--z1e;
penstroke z6e--z7e; 
serif(7,6,a,jut,band);
serif(6,7,b,-jut,band);
penlabels(range 1 thru 7);
endchar;

% c
newchar("0063",340u#+.5stem#,ht_x#,0,
 .75gap#+.35jut#,.75gap#+.35jut#);
x3r=l;
x1r=x5r=r;
x2=x4=.5[x3,r];
y2r=h;
y4r=0;
y3=.5h;
y1r=.7h;
y5r=.3h;
pos1(stem,up,0);
pos2(band,left,90);
pos3(stem,down,180);
pos4(band,right,-90);
pos5(stem,up,0);
penstroke arc.e(1,2) & arc.e(2,3) & arc.e(3,4) & arc.e(4,5);
charanchorcedillas_[charepscode]=z4r;
charanchortops_[charepscode]=(x2,h);
penlabels(range 1 thru 5);
endchar;

% d
newchar("0064",310u#+stem#,ht_asc#,0,
 .75gap#+.35jut#,gap#+jut#);
x6r=x7r=r;
x1=x5=x6;
x3r=l;
x2=x4=.5[x3l,x6l];
y4r=y5r=y6r=0;
y1r=y2r=ht_x;
y7r=h;
y3r=.5[y4,y2];
pos1(band,left,90);
pos2(band,left,90);
pos3(stem,down,180);
pos4(band,right,-90);
pos5(band,right,-90);
pos6(stem,up,0);
pos7(stem,up,0);
penstroke z1e--z2e & arc.e(2,3) & arc.e(3,4) & z4e--z5e;
penstroke z6e--z7e; 
serif(7,6,a,-jut,band);
serif(6,7,b,-jut,band);
charanchortoprights_[charepscode]=(x6r+40u,ht_x);
charanchorbots_[charepscode]=(x5r,0);
penlabels(range 1 thru 7);
endchar;

% e
newchar("0065",320u#+stem#,ht_x#,0,
 .75gap#+.35jut#,.75gap#+.35jut#);
x5r=l;
x3r=r;
x1=x5;
x2=x3=x7;
x4=x6=.5[l,r];
y1=y2=.52h;
y4r=h;
y5r=y3=y1r;
y6r=0;
y7=.25h;
pos1(band,right,-90);
pos2(band,right,-90);
pos3(stem,up,0);
pos4(band,left,90);
pos5(stem,down,180);
pos6(band,right,-90);
pos7(stem,up,0);
penstroke z1e--z2e;
penstroke arc.e(3,4) & arc.e(4,5) & arc.e(5,6) & arc.e(6,7);
charanchortops_[charepscode]=(.5[x1,x2],h);
charanchorbots_[charepscode]=(.6[x1,x2],0);
charanchorcedillas_[charepscode]=(.7[x6,x7],0);
penlabels(range 1 thru 7);
endchar;

% f
newchar("0066",130u#+stem#,ht_asc#,0,
 gap#+jut#,.5gap#*mono);
if mono=1:
 x4r=r;
 x3=.8[x1,x4];
else:
 x3=r;
 x4r=1.2[x1,x3];
fi 
x1r=x2r=l;
x2r-x5l=.5(x3-x1);
x6l-x2l=.6(x3-x1);
y1=0;
y2=.7h;
y5l=y6l=ht_x;
y3r=y4r=h;
pos1(stem,down,180);
pos2(stem,down,180);
pos3(band,left,90);
pos4(band,left,90);
pos5(band,right,-90);
pos6(band,right,-90);
penstroke z4e--z3e & arc.e(3,2) & z2e--z1e;
penstroke z5e--z6e;
serifs(2,1,a,b,.9jut,1.1jut,band);
penlabels(range 1 thru 6);
endchar;

% g
newchar("0067",310u#+stem#,ht_x#,depth_desc#,
 .75gap#+.35jut#,gap#+jut#);
x6r=x7r=r;
x1=x5=x6;
x3r=l;
x2=x4=x8=.5[l,r];
x9=.5[x3r,x3];
y1r=y2r=y6=h;
y3=.5[y2,y4];
y4r=y5r=y7=0;
y9r=y8r=-d;
pos1(band,left,90);
pos2(band,left,90);
pos3(stem,down,180);
pos4(band,right,-90);
pos5(band,right,-90);
pos6(stem,up,0);
pos7(stem,up,0);
pos8(band,right,-90);
pos9(band,right,-90);
penstroke z1e--z2e & arc.e(2,3) & arc.e(3,4) & z4e--z5e;
penstroke z9e--z8e & arc.e(8,7) & z7e--z6e;
serif(7,6,a,jut,band);
charanchortops_[charepscode]=(.5[l,r],h);
penlabels(range 1 thru 9);
endchar;

% h
newchar("0068",310u#+stem#,ht_asc#,0,
 gap#+jut#,.8gap#+jut#);
x1l=x2l=l;
x3=x2;
x5r=x6r=r;
x4=.5[x1,x6];
y1=y6=0;
y2=h;
y3r=y4r=ht_x;
y5=.5ht_x;
pos1(stem,up,0);
pos2(stem,up,0);
pos3(band,left,90);
pos4(band,left,90);
pos5(stem,up,0);
pos6(stem,up,0);
penstroke z1e--z2e;
penstroke z6e--z5e & arc.e(5,4) & z4e--z3e;
serif(1,2,a,-jut,band);
serifs(2,1,b,c,jut,1.1jut,band);
serifs(5,6,d,e,1.1jut,jut,band);
charanchortops_[charepscode]=(.6[x1,x6],ht_x);
penlabels(range 1 thru 6);
endchar;

% i
newchar("0069",stem#,ht_x#+ht_acc#,0,
 gap#+if (mono=1): 1.15 fi jut#,
 if (mono=1): .5 fi gap#+if (mono=1): .85 fi jut#);
z1l=(l,0);
z2l=(l,ht_x);
x3=x4=x1;
y3=y4-dotsize=.5[ht_x,h];
pos1(stem,up,0);
pos2(stem,up,0);
pos3(dotsize,up,0);
pos4(dotsize,up,0);
penstroke z1e--z2e;
penstroke z3e--z4e;
serifs(2,1,a,b,jut,jut,band);
serif(1,2,c,-jut,band);
charanchorbots_[charepscode]=z1;
charanchorcedillas_[charepscode]=z1;
penlabels(range 1 thru 4);
endchar;
 
% j
newchar("006A",stem#,ht_x#+ht_acc#,depth_desc#,
 gap#+jut#,if (mono=1): .5 fi gap# if (mono=0): +jut# fi);
x1l=x2l=l;
x4=x1l-if mono=0: 120u else: 240u fi;
x3=.5[x4,x1l];
x5=x6=x1;
y1l=-.4d;
y2l=ht_x;
y3r=y4r=-d;
y5=y6-dotsize=.5[ht_x,h];
pos1(stem,up,0);
pos2(stem,up,0);
pos3(band,right,-90);
pos4(band,right,-90);
pos5(dotsize,up,0);
pos6(dotsize,up,0);
penstroke z4e--z3e & arc.e(3,1) & z1e--z2e;
penstroke z5e--z6e;
serif(1,2,a,-jut,band);
penlabels(range 1 thru 6);
endchar;

% k
newchar("006B",290u#+1.8stem#,ht_asc#,0,
 gap#+jut#,.15jut#);
x1l=x2l=l;
x6r=r;
x3r=.95[x1,x6r];
x4l=x5l=x1;
y1=y6r=0;
y2=h;
y3r=ht_x;
y4l=y5l=.55ht_x;
pos1(stem,up,0);
pos2(stem,up,0);
posin3(1,.9stem,1,x3r-x4l,y3r-y4l);
pos4(.9stem,z3l-z4l,0);
posin6(1,.9stem,1,x6r-x5l,y6r-y5l);
pos5(.9stem,z6l-z5l,0);
z7=whatever[z4r,z3r]=whatever[z5r,z6r];
penstroke z1e--z2e;
serif(1,2,a,-jut,band);
serifs(2,1,b,c,jut,.8jut,band);
serifsarm(4,3,d,e,.8jut,.8jut,band);
serifsarm(5,6,f,g,.8jut,.8jut,band);
fill z7--z4r--z5r--cycle;
charanchorbots_[charepscode]=(.5[x1,x6l],0);
penlabels(range 1 thru 7);
endchar;

% l
newchar("006C",stem#,ht_asc#,0,
 gap#+jut#,gap#+jut#);
z1l=(l,0);
z2l=(l,h);
pos1(stem,up,0);
pos2(stem,up,0);
penstroke z1e--z2e; 
serifs(2,1,a,b,jut,jut,band);
serif(1,2,c,-jut,band);
charanchortops_[charepscode]=z2;
charanchorbots_[charepscode]=z1;
charanchortoprights_[charepscode]=(x1r+40u,ht_x);
penlabels(range 1 thru 2);
endchar;

% m
newchar("006D",490u#+2stem#,ht_x#,0,
 gap#+jut#,.8gap#+jut#);
x1l=x2l=l;
x5=x2;
x7r=x8r=r;
x3=x4=.5[x1,x8];
x6=.5[x4,x8];
y1l=y3=y8r=0;
y2l=y5r=y4=y6r=h;
y7r=.5h;
pos1(stem,up,0);
pos2(stem,up,0);
pos3(stem,up,0);
pos4(stem,up,0);
pos5(band,left,90);
pos6(band,left,90);
pos7(stem,up,0);
pos8(stem,up,0);
penstroke z1e--z2e;
penstroke z3e--z4e;
penstroke z8e--z7e & arc.e(7,6) & z6e--z5e;
serif(1,2,a,-jut,band);
serifs(2,1,b,c,jut,jut,band);
if mono=0:
 serifs(4,3,d,e,.9jut,.9jut,band); 
 serifs(7,8,f,g,jut,jut,band);
else:
 serif(4,3,d,-.9jut,band); 
 serif(7,8,e,-jut,band);
fi
penlabels(range 1 thru 8);
endchar;

% n
newchar("006E",310u#+stem#,ht_x#,0,
 gap#+jut#,.8gap#+jut#);
x1l=x2l=l;
x3=x2;
x5r=x6r=r;
x4=.5[x1,x6];
y1=y6=0;
y2=y3r=y4r=h;
y5=.5h;
pos1(stem,up,0);
pos2(stem,up,0);
pos3(band,left,90);
pos4(band,left,90);
pos5(stem,up,0);
pos6(stem,up,0);
penstroke z1e--z2e;
penstroke z6e--z5e & arc.e(5,4) & z4e--z3e;
serif(1,2,a,-jut,band);
serifs(2,1,b,c,jut,1.1jut,band);
serifs(5,6,d,e,1.1jut,jut,band);
charanchortops_[charepscode]=(.5[x1,x6],ht_x);
charanchorbots_[charepscode]=(.5[x1,x6],0);
penlabels(range 1 thru 6);
endchar;

% o
newchar("006F",330u#+stem#,ht_x#,0,
 .75gap#+.35jut#,.75gap#+.35jut#);
z1r=(r,.5h);
z2r=(.5w,h);
z3r=(l,.5h);
z4r=(.5w,0);
pos1(stem,up,0);
pos2(band,left,90);
pos3(stem,down,180);
pos4(band,right,-90);
penstroke arc.e(1,2) & arc.e(2,3) & arc.e(3,4) & arc.e(4,1) & cycle; 
charanchortops_[charepscode]=(x2r,h);
charanchorbots_[charepscode]=(x4r,0);
penlabels(range 1 thru 4);
endchar;

% p
newchar("0070",310u#+stem#,ht_x#,depth_desc#,
 gap#+jut#,.75gap#+.35jut#);
x6l=x7l=l;
x1=x5=x6;
x3r=r;
x2=x4=.5[x3,x6l];
y4r=y5r=0;
y1r=y2r=y7l=h;
y6l=-d;
y3r=.5[y4,y2];
pos1(band,left,90);
pos2(band,left,90);
pos3(stem,up,0);
pos4(band,right,-90);
pos5(band,right,-90);
pos6(stem,up,0);
pos7(stem,up,0);
penstroke z5e--z4e & arc.e(4,3) & arc.e(3,2) & z2e--z1e;
penstroke z6e--z7e; 
serifs(7,6,a,b,jut,jut,band);
serif(6,7,c,-jut,band);
penlabels(range 1 thru 7);
endchar;

% q
newchar("0071",310u#+stem#,ht_x#,depth_desc#,
 .75gap#+.35jut#,gap#+jut#);
x6r=x7r=r;
x1=x5=x6;
x3r=l;
x2=x4=.5[x3,x6l];
y4r=y5r=0;
y1r=y2r=h;
y6r=-d;
y7r=h;
y3r=.5[y4,y2];
pos1(band,left,90);
pos2(band,left,90);
pos3(stem,down,180);
pos4(band,right,-90);
pos5(band,right,-90);
pos6(stem,up,0);
pos7(stem,up,0);
penstroke z1e--z2e & arc.e(2,3) & arc.e(3,4) & z4e--z5e;
penstroke z6e--z7e; 
serifs(7,6,a,b,jut,jut,band);
serif(6,7,c,jut,band);
penlabels(range 1 thru 7);
endchar;

% r
newchar("0072",220u#+stem#,ht_x#,0,
 gap#+jut#,.6gap#);
x1l=x2l=l;
x3=x2;
x5r=r;
x4r=.4[x1,x5];
y1l=0;
y2l=y3r=y4r=h;
y5r=.7h;
pos1(stem,up,0);
pos2(stem,up,0);
pos3(band,left,90);
pos4(band,left,90);
pos5(stem,up,0);
penstroke z1e--z2e;
penstroke z5e & arc.e(5,4) & z4e--z3e;
serif(1,2,a,-jut,band);
serifs(2,1,b,c,jut,1.1jut,band);
charanchortops_[charepscode]=(.2[x3,x5],h);
charanchorbots_[charepscode]=(.2[x3,x5],0);
penlabels(range 1 thru 5);
endchar;

% s
newchar("0073",310u#+stem#,ht_x#,0,
 .75gap#+.35jut#,.75gap#+.35jut#);
x3r=x8r=l;
x6r=r;
x2=x4=x5=x7=.5[l,r];
x1r=.98[l,r];
y2r=h;
y7r=0;
y4=y5=.5h;
y1r=y3=.5[y4,y2];
y6=y8r=.5[y7,y5];
pos1(stem,up,0);
pos2(band,left,90);
pos3(stem,down,180);
pos4(band,right,-90);
pos5(band,left,90);
pos6(stem,up,0);
pos7(band,right,-90);
pos8(stem,down,180);
penstroke arc.e(1,2) & arc.e(2,3) & arc.e(3,4);
penstroke arc.e(8,7) & arc.e(7,6) & arc.e(6,5);
charanchortops_[charepscode]=(x7,h);
charanchorbots_[charepscode]=(x7,0);
charanchorcedillas_[charepscode]=(x7,0);
penlabels(range 1 thru 8);
endchar;

% t
newchar("0074",180u#+.5stem#,.65[ht_x#,ht#],0,
 gap#+.85jut#,.5gap#);
x1r=x2r=l;
x4=r;
x3=.7[x2,x4];
x1r-x5=.4(x4-x2);
x6-x1l=.7(x4-x2);
y1=h;
y2=.4ht_x;
y3r=y4r=0;
y5l=y6l=ht_x;
pos1(stem,down,180);
pos2(stem,down,180);
pos3(band,right,-90);
pos4(band,right,-90);
pos5(band,right,-90);
pos6(band,right,-90);
penstroke z1e--z2e & arc.e(2,3) & z3e--z4e; 
penstroke z5e--z6e;
charanchortops_[charepscode]=(0,h);
charanchorbots_[charepscode]=(.5[x2,x4],0);
penlabels(range 1 thru 6);
endchar;

% u
newchar("0075",300u#+stem#,ht_x#,0,
 .75gap#+jut#,gap#+jut#);
x1r=x2r=l;
x5r=x6r=r;
x3=.5[x1,x4];
y1r=y6r=h;
y3r=y5r=0;
y2r=.5h;
z4r=z5;
pos1(stem,down,180);
pos2(stem,down,180);
pos3(band,right,-90);
pos4(band,right,-90);
pos5(stem,up,0);
pos6(stem,up,0);
penstroke z5e--z6e;
penstroke z1e--z2e & arc.e(2,3) & z3e--z4e;
serif(2,1,a,-jut,band);
serif(5,6,b,-jut,band);
serifs(6,5,c,d,jut,jut,band);
charanchortops_[charepscode]=(.5[l,r],h);
charanchorbots_[charepscode]=(.5[l,r],0);
charanchorcedillas_[charepscode]=z5;
penlabels(range 1 thru 6);
endchar;

% v
newchar("0076",380u#+stem#,ht_x#,0,
 .1gap#+.7jut#,.1gap#+.7jut#);
z1l=(l,h); 
z4r=(r,h); 
z5=(.5[x1l,x4r],min(band,.9stem));
y2r=y3l=0; 
posin1(1,.9stem,1,x5-x1l,y5-y1l);
posin4(1,.9stem,1,x5-x4r,y5-y4r);
z2r=whatever[z1r,z5];
z3l=whatever[z4l,z5];
posin2(1,.9stem,1,x5-x1l,y5-y1l);
posin3(1,.9stem,1,x5-x4r,y5-y4r);
serifsarm(2,1,a,b,.8jut,.8jut,band);
serifsarm(3,4,c,d,.8jut,.8jut,band);
penlabels(range 1 thru 5); 
endchar;

% w
newchar("0077",570u#+1.2stem#,ht_x#,0,
 .1gap#+.8jut#,.1gap#+.8jut#);
z1l=(l,h); 
z4r=(.5[l,r]+.2stem,if mono=1: .6h else: h fi); 
z5=(.5[x1l,x4r],min(band,.9stem));
y2r=y3l=0; 
posin1(1,.9stem,1,x5-x1l,y5-y1l);
posin4(1,.9stem,1,x5-x4r,y5-y4r);
z2r=whatever[z1r,z5];
z3l=whatever[z4l,z5];
posin2(1,.9stem,1,x5-x1l,y5-y1l);
posin3(1,.9stem,1,x5-x4r,y5-y4r);
z6l=(r-x4r+l,y4r);
z6r=(r-x4l+l,y4l);
z7l=(r-x3r+l,y3r);
z7r=(r-x3l+l,y3l);
z8l=(r-x2r+l,y2r);
z8r=(r-x2l+l,y2l);
z9l=(r-x1r+l,y1r);
z9r=(r-x1l+l,y1l);
z8=.5[z8l,z8r];
z9=.5[z9l,z9r];
penstroke z3e--z4e;
penstroke z6e--z7e;
serifsarm(2,1,a,b,.8jut,.8jut,band);
serifsarm(8,9,c,d,.8jut,.8jut,band);
charanchortops_[charepscode]=(.5[x1,x9],h);
charanchorbots_[charepscode]=(.5[x1,x9],0);
if mono=1: stem:=stem/.9; fi
penlabels(range 1 thru 9); 
endchar;

% x
newchar("0078",320u#+2stem#,ht_x#,0,
 .1gap#+if (mono=1): .5 else: .1 fi jut#,
 .1gap#+if (mono=1): .5 else: .1 fi jut#);
x1l=x3l=l;
x2r=x4r=r;
y1l=y4r=0;
y2r=y3l=h;
posin1(1,.9stem,1,x2r-x1l,y2r-x1l);
posin2(1,.9stem,1,x2r-x1l,y2r-x1l);
posin3(1,.9stem,1,x4r-x3l,y4r-y3l);
posin4(1,.9stem,1,x4r-x3l,y4r-y3l);
serifsarms(2,1,a,b,c,d,.8jut,.5jut,band);
serifsarms(3,4,e,f,g,h,.5jut,.8jut,band);
charanchortops_[charepscode]=(.5[l,r],h);
charanchorbots_[charepscode]=(.5[l,r],0);
penlabels(range 1 thru 4); 
endchar;

% y
newchar("0079",380u#+stem#,ht_x#,depth_desc#,
 .1gap#+.7jut#,.1gap#+.7jut#);
% as in v
z1l=(l,h); 
z4r=(r,h); 
z5=z6r=(.5[x1l,x4r],.2h);
y2r=y3l=0; 
posin1(1,.9stem,1,x5-x1l,y5-y1l);
posin4(1,.9stem,1,x5-x4r,y5-y4r);
z2r=whatever[z1r,z5];
z3l=whatever[z4l,z5];
posin2(1,.9stem,1,x5-x1l,y5-y1l);
posin3(1,.9stem,1,x5-x4r,y5-y4r);
% additional
y7=y7l=y7r=-d;
z6l=whatever[z1l,z2l]=whatever[z3l,z4l];
z6=.5[z6l,z6r];
z7=whatever[z3,z4];
z7l=whatever[z3l,z4l];
z7r=whatever[z3r,z4r];
serifsarm(6,1,a,b,.8jut,.8jut,band);
serifsarm(3,4,c,d,.8jut,.8jut,band);
serifsarm(4,7,e,f,1.1jut,.9jut,band);
charanchortops_[charepscode]=(.5[x1,x4],h);
charanchorbots_[charepscode]=(.5[x1,x4],-d);
penlabels(range 1 thru 7); 
endchar;

% z
newchar("007A",420u#,ht_x#,0,
 .7gap#+.25jut#,.7gap#+.25jut#);
x4l=x5=l;
x6=r;
x1=.05[l,r];
x2=x3r=.98[l,r];
y1l=y2l=h;
y3r=y2r;
y4l=y5l;
y5r=y6r=0;
pos1(band,right,-90);
pos2(band,right,-90);
pos5(band,right,-90);
pos6(band,right,-90);
posin3(1,.9stem,1,x3r-x4l,y3r-y4l);
posin4(1,.9stem,1,x3r-x4l,y3r-y4l);
penstroke z1e--z2e;
penstroke z3e--z4e;
penstroke z5e--z6e;
serif(2,1,a,-jut,stem);
serif(5,6,b,-1.1jut,stem);
charanchortops_[charepscode]=(.5[l,r],h);
charanchorbots_[charepscode]=(.5[l,r],0);
penlabels(range 1 thru 6); 
endchar;

% zero
newchar("0030",500u#,ht_digits#,0,0,0);
z1r=(.9w,.5h);
z2r=(.5w,h);
z3r=(.1w,.5h);
z4r=(.5w,0);
pos1(stem,up,0);
pos2(band,left,90);
pos3(stem,down,180);
pos4(band,right,-90);
penstroke arc.e(1,2) & arc.e(2,3) & arc.e(3,4) & arc.e(4,1) & cycle; 
if mono=1:
 z5r=point .5 of arc.l(1,2);
 z6l=point .5 of arc.l(3,4);
 pos5(.8stem,z6l-z5r,-90); % this is slightly inexact
 pos6(.8stem,z6l-z5r,-90); % this is slightly inexact
 penstroke z5e--z6e;
 penlabels(range 1 thru 6);
else:
 penlabels(range 1 thru 4);
fi
endchar;

% one
newchar("0031",500u#,ht_digits#,0,0,0);
x1=x2=.52w;
x3l=.12w;
x4l=x2l;
y1=0;
y2=y4l=h;
y3l=.9h;
pos1(stem,up,0);
pos2(stem,up,0);
pos3(.9stem,z4l-z3l,-90);
pos4(.9stem,z4l-z3l,0);
z5=whatever[z3r,z4r]=whatever[z1l,z2l];
fill z3r--z5--z1l--z1r--z2r--z2l--z3l--cycle;
serifs(2,1,a,b,1.8jut,1.8jut,band);
penlabels(range 1 thru 5);
endchar;

% two
newchar("0032",500u#,ht_digits#,0,0,0);
x8r=x6r=x1r=.1w;
x3r=x9r=.9w;
x2r=x4=x5=.5[x3r,x1r];
y2r=h;
y1r=y3r=.75h;
y4=y5=.48h;
y6l=.2h;
y8r=y9r=0;
z7r=z8r;
pos1(stem,down,180);
pos2(band,left,90);
pos3(stem,up,0);
pos6(stem,down,180);
pos7(stem,down,180);
pos8(band,right,-90);
pos9(band,right,-90);
pos4(band,right,-90);
pos5(band,left,90);
penstroke z8e--z9e;
penstroke arc.e(4,3) & arc.e(3,2) & arc.e(2,1);
penstroke arc.e(5,6) & z6e--z7e;
serif(8,9,a,-1.5jut,stem);
penlabels(range 1 thru 9);
endchar;

% three
newchar("0033",500u#,ht_digits#,0,0,0);
x1r=x9r=.1w;
x3r=x7r=.9w;
x2r=x8r=x4r=x6r=.5[x3r,x1r];
x5=.8[x1r,x4];
y2r=h;
y1r=y3r=.5[y2,y4];
y7r=y9r=.5[y6,y8];
y8r=0;
y5=.52h;
y6l=y5r;
y4l=y5l;
pos1(stem,down,180);
pos2(band,left,90);
pos3(stem,up,0);
pos4(.6band,right,-90);
pos5(band,right,-90);
pos6(.6band,left,90);
pos7(stem,up,0);
pos8(band,right,-90);
pos9(stem,down,180);
penstroke arc.e(4,3) & arc.e(3,2) & arc.e(2,1);
penstroke arc.e(9,8) & arc.e(8,7) & arc.e(7,6);
fill z5l--z5r--z6l--z4l--cycle;
penlabels(range 1 thru 9);
endchar;

% four
newchar("0034",500u#,ht_digits#,0,0,0);
x1=x2=.7w;
x3=.05w;
x4=.95w;
x7=x2l;
y1=0;
y2=h;
y3=y4=.35h;
y7=h-band;
y6r=h;
z5l=z3l;
pos1(stem,up,0);
pos2(stem,up,0);
pos3(band,right,-90);
pos4(band,right,-90);
posin5(1,.9stem,1,x7-x5l,y7-y5l);
posin6(1,.9stem,1,x7-x5l,y7-y5l);
z6r=whatever[z5r,z7];
penstroke z1e--z2e;
penstroke z3e--z4e;
fill z5l--z5r--z7--z2--z6l--cycle;
serifs(2,1,a,b,1.3jut,1.1jut,band);
penlabels(range 1 thru 7);
endchar;

% five
newchar("0035",500u#,ht_digits#,0,0,0);
x1r=.9w;
x3r=.1w;
x6l=x7l=x9r=x8l=.12w;
x2=x4r=.5w;
x5=.88w;
y2=.53h;
y1r=y3r=.5[y4,y2];
y4r=0;
y9r=y8l=y2r;
y7l=y6l=y5l=h;
pos2(band,left,90);
pos4(band,right,-90);
pos1(stem,up,0);
pos3(stem,down,180);
pos5(band,right,-90);
pos6(band,right,-90);
pos7(stem,up,0);
pos8(stem,up,0);
pos9(band,left,90);
penstroke arc.e(3,4) & arc.e(4,1) & arc.e(1,2) & z2e--z9e;
penstroke z7e--z8e;
penstroke z5e--z6e;
serif(6,5,a,jut,stem);
penlabels(range 1 thru 9);
endchar;

% six
newchar("0036",500u#,ht_digits#,0,0,0);
z1r=(.9w,.5[y2,y4]);
z2=(.5w,.52h);
z3r=(.1w,.5[y2,y4]);
z4r=(.5w,0);
z5r=(.88w,.5[y2,y6]);
z6r=(.5w,h);
z7r=(.1w,y5r);
z8l=z3l;
pos1(stem,up,0);
pos2(band,left,90);
pos3(stem,down,180);
pos4(band,right,-90);
pos5(stem,up,0);
pos6(band,left,90);
pos7(stem,down,180);
pos8(.4stem,down,180);
penstroke arc.e(5,6) & arc.e(6,7) & z7e--z3e 
 & arc.e(3,4) & arc.e(4,1) & arc.e(1,2) & arc.e(2,8);
penlabels(range 1 thru 8);
endchar;

% seven
newchar("0037",500u#,ht_digits#,0,0,0);
z1l=(.1w,h);
z2l=(.9w,h);
z3r=z2r;
z4r=(.52w,0);
pos1(band,right,-90);
pos2(band,right,-90);
pos3(stem,z3r-z4r,0);
pos4(stem,z3r-z4r,0);
penstroke z1e--z2e;
penstroke z3e--z4e;
serif(2,1,a,-jut,stem);
penlabels(range 1 thru 4);
endchar;

% eight
newchar("0038",500u#,ht_digits#,0,0,0);
z1r=(.9w,.5[y2,y4]);
z2r=(.5w,.52h+.1band);
z3r=(.1w,.5[y2,y4]);
z4r=(.5w,0);
z5r=(.9w,.5[y6,y8]);
z6r=(.5w,h);
z7r=(.1w,.5[y6,y8]);
z8r=(.5w,.52h-.1band);
pos1(stem,up,0);
pos2(.6band,left,90);
pos3(stem,down,180);
pos4(band,right,-90);
pos5(stem,up,0);
pos6(band,left,90);
pos7(stem,down,180);
pos8(.6band,right,-90);
penstroke arc.e(1,2) & arc.e(2,3) & arc.e(3,4) & arc.e(4,1) & cycle; 
penstroke arc.e(5,6) & arc.e(6,7) & arc.e(7,8) & arc.e(8,5) & cycle; 
penlabels(range 1 thru 8);
endchar;

% nine
newchar("0039",500u#,ht_digits#,0,0,0);
z1r=(.1w,.5[y2,y4]);
z2=(.5w,.48h);
z3r=(.9w,.5[y2,y4]);
z4r=(.5w,h);
z5r=(.12w,.5[y2,y6]);
z6r=(.5w,0);
z7r=(.9w,y5r);
z8l=z3l;
pos1(stem,down,180);
pos2(band,right,-90);
pos3(stem,up,0);
pos4(band,left,90);
pos5(stem,down,180);
pos6(band,right,-90);
pos7(stem,up,0);
pos8(.4stem,up,0);
penstroke arc.e(5,6) & arc.e(6,7) & z7e--z3e 
 & arc.e(3,4) & arc.e(4,1) & arc.e(1,2) & arc.e(2,8);
penlabels(range 1 thru 8);
endchar;

% space
newchar("0020",font_normal_space_,ht#,0,0,0);
endchar;

% period
newchar("002E",dotsize#,ht_x#,0,
 .75gap#+.35jut#,.75gap#+.35jut#);
x1=x2=.5[l,r];
y2=0;
y1=dotsize;
pos1(dotsize,up,0);
pos2(dotsize,up,0);
penstroke z1e--z2e;
penlabels(range 1 thru 2);
endchar;

% colon
newchar("003A",dotsize#,ht_x#,0,
 .75gap#+.35jut#,.75gap#+.35jut#);
x1=x2=x3=x4=.5[l,r];
y2=h-y4=0;
y1=h-y3=dotsize;
pos1(dotsize,up,0);
pos2(dotsize,up,0);
pos3(dotsize,up,0);
pos4(dotsize,up,0);
penstroke z1e--z2e;
penstroke z3e--z4e;
penlabels(range 1 thru 4);
endchar;

% semicolon
newchar("003B",dotsize#,ht_x#,.5depth_desc#,
 .75gap#+.35jut#,.75gap#+.35jut#);
x1=x2=x5=x6=.5[l,r];
x4l=x2l;
y2=h-y6=0;
y4l=-d;
y1=h-y5=dotsize;
z3r=z2r;
pos1(dotsize,up,0);
pos2(dotsize,up,0);
posin3(1,.5dotsize,1,x3r-x4l,y3r-y4l);
posin4(1,.5dotsize,1,x3r-x4l,y3r-y4l);
pos5(dotsize,up,0);
pos6(dotsize,up,0);
penstroke z1e--z2e--z3e--z4e;
penstroke z5e--z6e;
penlabels(range 1 thru 6);
endchar;

% comma
newchar("002C",dotsize#,ht_x#,.5depth_desc#,
 .75gap#+.35jut#,.75gap#+.35jut#);
x1=x2=.5[l,r];
x4l=x2l;
y2=0;
y4l=-d;
y1=dotsize;
z3r=z2r;
pos1(dotsize,up,0);
pos2(dotsize,up,0);
posin3(1,.5dotsize,1,x3r-x4l,y3r-y4l);
posin4(1,.5dotsize,1,x3r-x4l,y3r-y4l);
penstroke z1e--z2e--z3e--z4e;
penlabels(range 1 thru 4);
endchar;

% exclam
newchar("0021",.95dotsize#,ht#,0,
 gap#+.5jut#,gap#+.5jut#);
x1=x2=x3=x4=.5[l,r];
y2=0;
y1=.95dotsize;
y3=h;
y4=2.5dotsize;
pos1(.95dotsize,up,0);
pos2(.95dotsize,up,0);
pos3(dotsize,up,0);
pos4(.7dotsize,up,0);
penstroke z1e--z2e;
penstroke z3e--z4e;
penlabels(range 1 thru 4);
endchar;

% question
newchar("003F",350u#+.5stem#,ht#,0,
 .75gap#+.35jut#,.75gap#+.35jut#);
x1r=l;
x3r=r;
x2r=.5[x1r,x3];
x4l=x5=x6=x7=x8=.45[x1r,x3];
y2r=h;
y4l=y5=.5h;
y1r=y3r=.5[y4,y2];
y6=1.6dotsize;
y7=0;
y8=.95dotsize;
pos1(stem,down,180);
pos2(band,left,90);
pos4(band,right,-90);
pos3(stem,up,0);
pos5(stem,up,0);
pos6(stem,up,0);
pos7(.95dotsize,up,0);
pos8(.95dotsize,up,0);
penstroke arc.e(4,3) & arc.e(3,2) & arc.e(2,1);
penstroke z5e--z6e;
penstroke z7e--z8e;
penlabels(range 1 thru 8);
endchar;

% hyphen
newchar("002D",240u#,ht#,0,gap#,gap#);
x1=l;
x2=r;
y1=y2=ht_math;
pos1(band,right,-90);
pos2(band,right,-90);
penstroke z1e--z2e;
penlabels(range 1 thru 2);
endchar;
